apply plugin: 'java'
apply plugin: 'java-gradle-plugin'

def pom = new XmlSlurper().parse(file('./pom.xml'))

group = pom.parent.groupId.text().toString()
version = pom.parent.version.text().toString()

description = pom.description.text().toString()

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

repositories {
    mavenLocal()
    mavenCentral()
}

def extras = Boolean.getBoolean('net.bytebuddy.misc.extras');

dependencies {
    compile gradleApi()
    // At this point, it is not given that any artifact from the Maven build can be found in a repository.
    def artifact = extras ? 'byte-buddy' : 'byte-buddy-dep'
    logger.info("Using ${artifact} as Byte Buddy dependency")
    def location = "../${artifact}/target/${artifact}-${version}.jar"
    if (new File(location).exists()) {
        logger.info('Byte Buddy location does not exist, cannot only clean but not build project')
    } else {
        compile files(location)
    }
    // When using the non-shaded dependency, the ASM dependencies are required.
    if (!extras) {
        def asmVersion = new XmlSlurper().parse(file('../pom.xml')).properties.'version.asm'
        compile group: 'org.ow2.asm', name: 'asm', version: asmVersion
        compile group: 'org.ow2.asm', name: 'asm-commons', version: asmVersion
    }
    testCompile gradleTestKit()
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
}

// Without the extras property, creating a javadoc artifact is not necessary.
if (extras) {
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
} else {
    task javadocJar {
        logger.info('javadoc is only generated if net.bytebuddy.misc.extras is set to true')
    }
}

test {
    systemProperty('net.bytebuddy.test.version', version)
    systemProperty('net.bytebuddy.test.integration', Boolean.getBoolean('net.bytebuddy.test.integration'))
}
