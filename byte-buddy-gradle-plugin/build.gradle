apply plugin: 'java'
apply plugin: 'java-gradle-plugin'

def pom = new XmlSlurper().parse(file('./pom.xml'))

group = pom.parent.groupId.text().toString()
version = pom.parent.version.text().toString()

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile group: group, name: 'byte-buddy', version: version
    testCompile gradleTestKit()
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
}

if (Boolean.getBoolean('net.bytebuddy.test.extras')) {
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
} else {
    task javadocJar {
        logger.info('javadoc is only generated if net.bytebuddy.test.extras is set to true')
    }
}

test {
    systemProperty('net.bytebuddy.test.version', version)
    systemProperty('net.bytebuddy.test.integration', Boolean.getBoolean('net.bytebuddy.test.integration'))
}
